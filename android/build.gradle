// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        minSdkVersion = 19
        targetSdkVersion = 26
        compileSdkVersion = 27

        supportLibVersion = "27.1.1"
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        jcenter()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
    }
}

// Force all our third-party dependencies to a sensible buildToolsVersion.
// What version should that be?  It's whatever the Android plugin applied
// to our app project.
project(":app").afterEvaluate {
    ext.buildToolsVersion = it.android.buildToolsVersion
}
allprojects {
    if (path != ":" && path != ":app") {
        // This line is tricky -- it causes :app to be (sort of?) evaluated,
        // so the `project(":app").afterEvaluate` must come first.
        evaluationDependsOn(":app")
    }
    afterEvaluate {
        // The plugins for an Android app, library, etc., all pull in
        // a common base plugin; look for that to identify Android subprojects
        // (where the `android` property will exist.)
        if (plugins.any { it.class.name == "com.android.build.gradle.api.AndroidBasePlugin" }) {
            android.buildToolsVersion rootProject.ext.buildToolsVersion
        }
    }
}
